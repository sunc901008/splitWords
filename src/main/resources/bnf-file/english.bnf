<question> := <filter> |
              <phrase>;

<filter> := <number-simple-filter> |
            <date-simple-filter> |
            <date-complex-filter> |
            <string-simple-filter> |
            <string-complex-filter> |
            <bool-function-column>;

<phrase> := <top-n> |
            <bottom-n> |
            <sort-by> |
            <growth-of> |
            <all-columns> |
            <number-complex-phrase> |
            <formula-column>;

<number-simple-filter> := <number-columns> <bool-symbol> <number> |
                          <number> <bool-symbol> <number-columns> |
                          <number-columns> <bool-symbol> <number-columns> |
                          <number> <bool-symbol> <number>;

<number-complex-phrase> := <average-phrase>;

<date-simple-filter> := <date-columns> <bool-symbol> <date-string-value> |
                        <date-string-value> <bool-symbol> <date-columns> |
                        <date-columns> <bool-symbol> <date-columns> |
                        <date-string-value> <bool-symbol> <date-string-value>;

<date-complex-filter> := <before-after-filter> |
                         <last-filter> |
                         <between-and-filter> |
                         <date-interval> |
                         <ago-filter>;

<string-simple-filter> := <all-string-column> = <column-value>;

<string-complex-filter> := <begins-with-filter> |
                           <not-begins-with-filter> |
                           <contains-filter> |
                           <not-contains-filter>;

<top-n> := top <integer> <number-columns> |
           top <number-columns>;

<bottom-n> := bottom <integer> <number-columns> |
              bottom <number-columns>;

<sort-by> := sort by <all-columns> |
             sort by <all-columns> descending |
             sort by <all-columns> ascending;

<growth-of> := growth of <growth-of-measure> by <all-date-column> |
               growth of <growth-of-measure> by <all-date-column> <year-over-year> |
               growth of <growth-of-measure> by <all-date-column> <growth-of-by-date-interval> |
               growth of <growth-of-measure> by <all-date-column> <growth-of-by-date-interval> <year-over-year>;

<growth-of-measure> := <number-source-column> |
                       <growth-of-measure-operation> <number-source-column>;
<growth-of-measure-operation> := sum |
                                 average |
                                 count |
                                 max |
                                 min |
                                 standard deviation |
                                 unique count |
                                 variance;

<growth-of-by-date-interval> := daily |
                                weekly |
                                monthly |
                                quarterly |
                                yearly;

<year-over-year> := year over year;

<date-interval> := daily |
                   weekly |
                   monthly |
                   quarterly |
                   yearly |
                   by day of week |
                   by week |
                   by month |
                   <to-date-interval>;

<to-date-interval> := week to date |
                      month to date |
                      quarter to date |
                      year to date;

<ago-filter> := <ago-days-filter> |
               <ago-weeks-filter> |
               <ago-months-filter> |
               <ago-quarters-filter> |
               <ago-years-filter> |
               <ago-minutes-filter> |
               <ago-hours-filter>;

<ago-days-filter> := <integer> days ago;

<ago-weeks-filter> := <integer> weeks ago;

<ago-months-filter> := <integer> months ago;

<ago-quarters-filter> := <integer> quarters ago;

<ago-years-filter> := <integer> years ago;

<ago-minutes-filter> := <integer> minutes ago;

<ago-hours-filter> := <integer> hours ago;

<average-phrase> := average <number-source-column>;

<before-after-filter> := <before-filter> |
                         <after-filter>;

<before-filter> := before <date-columns> |
                   before <date-string-value> |
                   <all-date-column> before <date-columns>|
                   <all-date-column> before <date-string-value>;

<after-filter> := after <date-columns> |
                  after <date-string-value> |
                  <all-date-column> after <date-columns> |
                  <all-date-column> after <date-string-value>;

<last-filter> := <last-days-filter> |
                 <last-weeks-filter> |
                 <last-months-filter> |
                 <last-quarters-filter> |
                 <last-years-filter> |
                 <last-minutes-filter> |
                 <last-hours-filter>;

<last-minutes-filter> := last minute |
                        <all-date-column> last minute |
                        last <integer> minutes |
                        <all-date-column> last <integer> minutes;

<last-hours-filter> := last hour |
                        <all-date-column> last hour |
                        last <integer> hours |
                        <all-date-column> last <integer> hours;

<last-days-filter> := last day |
                      <all-date-column> last day |
                      last <integer> days |
                      <all-date-column> last <integer> days;

<last-weeks-filter> := last week |
                       <all-date-column> last week |
                       last <integer> weeks |
                       <all-date-column> last <integer> weeks;

<last-months-filter> := last month |
                        <all-date-column> last month |
                        last <integer> months |
                        <all-date-column> last <integer> months;

<last-quarters-filter> := last quarter |
                          <all-date-column> last quarter |
                          last <integer> quarters |
                          <all-date-column> last <integer> quarters;

<last-years-filter> := last year |
                       <all-date-column> last year |
                       last <integer> years |
                       <all-date-column> last <integer> years;

<between-and-filter> := <all-date-column> between <date-string-value> and <date-string-value>;

<begins-with-filter> := <all-string-column> begins with <column-value>;

<not-begins-with-filter> := <all-string-column> not begins with <column-value>;

<contains-filter> := <all-string-column> contains <column-value>;

<not-contains-filter> := <all-string-column> not contains <column-value>;

<all-columns> := <number-columns> |
                 <string-columns> |
                 <bool-columns> |
                 <date-columns>;

<number-columns> := <all-int-column> |
                    <all-double-column> |
                    <number-function-column> |
                    ( <number-function-column> );

<string-columns> := <all-string-column> |
                    <string-function-column> |
                    ( <string-function-column> );

<bool-columns> := <all-bool-column>;

<date-columns> := <all-date-column> |
                  <date-function-column> |
                  ( <date-function-column> );

<all-int-column> := <int-column> |
                    <table-int-column>;

<all-double-column> := <double-column> |
                       <table-double-column>;

<all-string-column> := <string-column> |
                       <table-string-column>;

<all-bool-column> := <bool-column> |
                     <table-bool-column>;

<all-date-column> := <date-column> |
                     <table-date-column>;

<all-source-column> := <number-source-column> |
                       <all-string-column> |
                       <all-bool-column> |
                       <all-date-column>;

<number-source-column> := <all-int-column> |
                          <all-double-column>;

<bool-symbol> := > | < | >= | <= | = | !=;
<math-symbol> := + | - | * | / | ^;
<number> := <integer> |
            <double>;
<integer> := 1;
<double> := 1;

<column-value> := <double-quote-column-value> |
                  <single-quote-column-value> |
                  <double-quote-column-value> , <column-value> |
                  <double-quote-column-value> ， <column-value> |
                  <single-quote-column-value> , <column-value> |
                  <single-quote-column-value> ， <column-value>;

<double-quote-column-value> := " <value> " |
                               “ <value> ”;
<single-quote-column-value> := ' <value> ' |
                               ‘ <value> ’;
<value> := 1;

<date-string-value> := <double-quote-date-string-value> |
                       <single-quote-date-string-value>;

<double-quote-date-string-value> := " <date-value> " |
                                    “ <date-value> ”;
<single-quote-date-string-value> := ' <date-value> ' |
                                    ‘ <date-value> ’;
<date-value> := 1;