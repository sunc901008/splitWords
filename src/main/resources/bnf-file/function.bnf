<number-function-column> := <average-function> |
                            <count-function> |
                            <max-function> |
                            <min-function> |
                            <sum-function> |
                            <to_double-function> |
                            <to_integer-function> |
                            <diff_days-function> |
                            <month_number-function> |
                            <year-function> |
                            <strlen-function> |
                            <number-function> |
                            <if-then-else-number-function> |
                            <ifnull-number-function>;

<bool-function-column> := <to_bool-function> |
                          <contains-function> |
                          <and-function> |
                          <or-function> |
                          <if-then-else-bool-column-function> |
                          <ifnull-bool-column-function> |
                          <isnull-function> |
                          <not-function>;

<date-function-column> := <to_date-function> |
                          <if-then-else-date-column-function> |
                          <ifnull-date-column-function>;

<string-function-column> := <to_string-function> |
                            <month-function> |
                            <concat-function> |
                            <substr-function> |
                            <if-then-else-string-function> |
                            <ifnull-string-function>;

# number
<average-function> := average ( <number-columns> ) |
                      average ( <number> );

# number
<count-function> := count ( <all-source-column> ) |
                    count ( <number> );

# number
<max-function> := max ( <all-date-column> ) |
                  max ( <number-columns> ) |
                  max ( <number> );

# number
<min-function> := min ( <all-date-column> ) |
                  min ( <number-columns> ) |
                  min ( <number> );

# number
<sum-function> := sum ( <number-columns> ) |
                  sum ( <number> );

# number
<to_double-function> := to_double ( <all-columns> );

#number
<to_integer-function> := to_integer ( <all-columns> );

# number
<diff_days-function> := diff_days ( <date-string-value> , <date-columns> ) |
                        diff_days ( <date-columns> , <date-columns> ) |
                        diff_days ( <date-columns> , <date-string-value> ) |
                        diff_days ( <date-string-value> , <date-string-value> );

# number
<month_number-function> := month_number ( <date-columns> ) |
                           month_number ( <date-string-value> );

# number
<year-function> := year ( <date-columns> ) |
                   year ( <date-string-value> );

# number
<strlen-function> := strlen ( <string-columns> ) |
                     strlen ( <column-value> );
# number
<if-then-else-number-function> := if <bool-columns> then <number> else <number> |
                                  if <no-or-and-bool-function-column> then <number> else <number> |
                                  if <bool-columns> then <number-columns> else <number-columns> |
                                  if <no-or-and-bool-function-column> then <number-columns> else <number-columns>;
# number
<ifnull-number-function> := ifnull ( <number> , <number> ) |
                            ifnull ( <number-columns> , <number-columns> );

# bool
<to_bool-function> := to_bool ( <number-columns> ) |
                      to_bool ( <number> );

# bool
<contains-function> := contains ( <string-columns> , <string-columns> ) |
                       contains ( <column-value> , <string-columns> ) |
                       contains ( <string-columns> , <column-value> ) |
                       contains ( <column-value> , <column-value> );

<no-or-and-bool-function-column> := <to_bool-function> |
                                  <contains-function> |
                                  <bool-function> |
                                  <if-then-else-bool-column-function> |
                                  <ifnull-bool-column-function> |
                                  <isnull-function> |
                                  <not-function> |
                                  <bool-columns>;
# bool
<and-function> := <bool-columns> and <no-or-and-bool-function-column> |
                  <bool-columns> and <and-function> |
                  <bool-columns> and <or-function> |
                  <no-or-and-bool-function-column> and <no-or-and-bool-function-column>;

# bool
<or-function> := <bool-columns> or <no-or-and-bool-function-column> |
                 <bool-columns> or <and-function> |
                 <bool-columns> or <or-function> |
                 <no-or-and-bool-function-column> or <no-or-and-bool-function-column>;
# bool
<if-then-else-bool-column-function> := if <bool-columns> then <bool-columns> else <no-or-and-bool-function-column> |
                                       if <bool-columns> then <no-or-and-bool-function-column> else <bool-columns> |
                                       if <no-or-and-bool-function-column> then <bool-columns> else <bool-columns> |
                                       if <bool-columns> then <no-or-and-bool-function-column> else <no-or-and-bool-function-column> |
                                       if <no-or-and-bool-function-column> then <bool-columns> else <no-or-and-bool-function-column> |
                                       if <no-or-and-bool-function-column> then <no-or-and-bool-function-column> else <bool-columns> |
                                       if <no-or-and-bool-function-column> then <no-or-and-bool-function-column> else <no-or-and-bool-function-column> |
                                       if <bool-columns> then <bool-columns> else <bool-columns>;

# bool
<isnull-function> := isnull ( <all-columns> ) |
                     isnull ( <number> );

# bool
<not-function> := not ( <bool-columns> ) |
                  not ( <no-or-and-bool-function-column> );

# bool
<ifnull-bool-column-function> := ifnull ( <bool-columns> , <bool-columns> ) |
                                 ifnull ( <no-or-and-bool-function-column> , <bool-columns> ) |
                                 ifnull ( <bool-columns> , <no-or-and-bool-function-column> ) |
                                 ifnull ( <no-or-and-bool-function-column> , <no-or-and-bool-function-column> );

# date
<to_date-function> := to_date ( <number-columns> ) |
                      to_date ( <number> );

# date
<if-then-else-date-column-function> := if <bool-columns> then <date-columns> else <date-columns> |
                                       if <no-or-and-bool-function-column> then <date-columns> else <date-columns>;

# date
<ifnull-date-column-function> := ifnull ( <date-columns> , <date-columns> );

# string
<to_string-function> := to_string ( <all-columns> ) |
                        to_string ( <number> );

# string
<month-function> := month ( <date-columns> ) |
                    month ( <date-string-value> );

# string
<concat-function> := concat ( <string-columns> , <string-columns> ) |
                     concat ( <column-value> , <string-columns> ) |
                     concat ( <string-columns> , <column-value> ) |
                     concat ( <column-value> , <column-value> );

# string
<substr-function> := substr ( <string-columns> , <integer> , <integer> ) |
                     substr ( <column-value> , <integer> , <integer> );

# string
<if-then-else-string-function> := if <bool-columns> then <column-value> else <column-value> |
                                  if <no-or-and-bool-function-column> then <column-value> else <column-value>|
                                  if <bool-columns> then <string-columns> else <string-columns> |
                                  if <no-or-and-bool-function-column> then <string-columns> else <string-columns>;

# string
<ifnull-string-function> := ifnull ( <column-value> , <column-value> ) |
                            ifnull ( <string-columns> , <string-columns> );

# filter number function itself
<no-number-function-column> := <average-function> |
                            <count-function> |
                            <max-function> |
                            <min-function> |
                            <sum-function> |
                            <to_double-function> |
                            <to_integer-function> |
                            <diff_days-function> |
                            <month_number-function> |
                            <year-function> |
                            <strlen-function> |
                            <ifnull-number-function>;

# number # todo base
<number-function> := <number> <math-symbol> <number-columns> |
                     #<number> <math-symbol> <number-function> |
                     <number> <math-symbol> <number> |
                     <number-source-column> <math-symbol> <number-columns> |
                     <number-source-column> <math-symbol> <number> |
                     <no-number-function-column> <math-symbol> <number-columns> |
                     <no-number-function-column> <math-symbol> <number>;

# bool # todo base
<bool-function> := <number-columns> <bool-symbol> <number> |
                   <number> <bool-symbol> <number-columns> |
                   <number-columns> <bool-symbol> <number-columns> |
                   <number> <bool-symbol> <number>;
