<number-function-column> := <average-function> |
                            <count-function> |
                            <max-function> |
                            <min-function> |
                            <sum-function> |
                            <to_double-function> |
                            <to_integer-function> |
                            <diff_days-function> |
                            <month_number-function> |
                            <year-function> |
                            <strlen-function> |
                            <number-function> |
                            <if-then-else-number-function> |
                            <if-then-else-number-column-function> |
                            <ifnull-number-function> |
                            <ifnull-number-column-function>;

<bool-function-column> := <to_bool-function> |
                          <contains-function> |
                          <bool-function> |
                          <and-function> |
                          <or-function> |
                          <if-then-else-bool-column-function> |
                          <ifnull-bool-column-function> |
                          <isnull-function> |
                          <not-function>;

<date-function-column> := <to_date-function> |
                          <if-then-else-date-column-function> |
                          <ifnull-date-column-function>;

<string-function-column> := <to_string-function> |
                            <month-function> |
                            <concat-function> |
                            <substr-function> |
                            <if-then-else-column-value-function> |
                            <if-then-else-string-column-function> |
                            <ifnull-column-value-function> |
                            <ifnull-string-column-function>;

# number
<average-function> := average ( <number-source-column> ) |
                      average ( <number> );

# number
<count-function> := count ( <all-source-column> );

# number
<max-function> := max ( <all-date-column> ) |
                  max ( <number-source-column> ) |
                  max ( <number> );

# number
<min-function> := min ( <all-date-column> ) |
                  min ( <number-source-column> ) |
                  min ( <number> );

# number
<sum-function> := sum ( <number-source-column> ) |
                  sum ( <number> );

# date
<to_date-function> := to_date ( <number-columns> );

# string
<to_string-function> := to_string ( <all-columns> ) |
                        to_string ( <number> );

# number
<to_double-function> := to_double ( <all-columns> );

#number
<to_integer-function> := to_integer ( <all-columns> );

# bool
<to_bool-function> := to_bool ( <number-columns> ) |
                      to_bool ( <number> );

# number
<diff_days-function> := diff_days ( <column-value> , <date-columns> ) |
                        diff_days ( <date-columns> , <date-columns> ) |
                        diff_days ( <date-columns> , <column-value> );

# string
<month-function> := month ( <date-columns> ) |
                    month ( <column-value> );

# number
<month_number-function> := month_number ( <date-columns> ) |
                           month_number ( <column-value> );

# number
<year-function> := year ( <date-columns> ) |
                   year ( <column-value> );

# string
<concat-function> := concat ( <string-columns> , <string-columns> ) |
                     concat ( <column-value> , <string-columns> ) |
                     concat ( <string-columns> , <column-value> ) |
                     concat ( <column-value> , <column-value> );

# bool
<contains-function> := contains ( <string-columns> , <string-columns> ) |
                       contains ( <column-value> , <string-columns> ) |
                       contains ( <string-columns> , <column-value> ) |
                       contains ( <column-value> , <column-value> );

# number
<strlen-function> := strlen ( <string-columns> ) |
                     strlen ( <column-value> );

# string
<substr-function> := substr ( <string-columns> , <integer> , <integer> ) |
                     substr ( <column-value> , <integer> , <integer> );

# number # todo base
<number-function> := <number> <math-symbol> <number-columns> |
                     <number-source-column> <math-symbol> <number> |
                     <number-source-column> <math-symbol> <number-source-column> |
                     <number> <math-symbol> <number>;

# bool # todo base
<bool-function> := <number> <bool-symbol> <number-columns> |
                   <number-source-column> <bool-symbol> <number> |
                   <number-source-column> <bool-symbol> <number-source-column> |
                   <number> <bool-symbol> <number>;

<no-or-and-bool-function-column> := <to_bool-function> |
                                  <contains-function> |
                                  <bool-function> |
                                  <if-then-else-bool-column-function> |
                                  <ifnull-bool-column-function> |
                                  <isnull-function> |
                                  <not-function>;

# bool
<and-function> := <all-bool-column> and <bool-columns> |
                  <all-bool-column> and <and-function> |
                  <all-bool-column> and <or-function> |
                  <no-or-and-bool-function-column> and <bool-columns>;

# bool
<or-function> := <all-bool-column> or <bool-columns> |
                 <all-bool-column> or <and-function> |
                 <all-bool-column> or <or-function> |
                 <no-or-and-bool-function-column> or <bool-columns>;

# string
<if-then-else-column-value-function> := if <bool-columns> then <column-value> else <column-value>;
# string
<if-then-else-string-column-function> := if <bool-columns> then <string-columns> else <string-columns>;
# number
<if-then-else-number-function> := if <bool-columns> then <number> else <number>;
# number
<if-then-else-number-column-function> := if <bool-columns> then <number-columns> else <number-columns>;
# bool
<if-then-else-bool-column-function> := if <bool-columns> then <bool-columns> else <bool-columns>;
# date
<if-then-else-date-column-function> := if <bool-columns> then <date-columns> else <date-columns>;

# string
<ifnull-column-value-function> := ifnull ( <column-value> , <column-value> );
# string
<ifnull-string-column-function> := ifnull ( <string-columns> , <string-columns> );
# number
<ifnull-number-function> := ifnull ( <number> , <number> );
# number
<ifnull-number-column-function> := ifnull ( <number-columns> , <number-columns> );
# bool
<ifnull-bool-column-function> := ifnull ( <bool-columns> , <bool-columns> );
# date
<ifnull-date-column-function> := ifnull ( <date-columns> , <date-columns> );

# bool
<isnull-function> := isnull ( <all-columns> ) |
                     isnull ( <number> );

# bool
<not-function> := not ( <bool-columns> );

# average、count、max、min、sum、to_date、to_string、to_double、to_integer、
# diff_days、month、month_number、year、
# !=、<、>、<=、>=、=、+、-、*、/、
# and、if...then...else、ifnull、isnull、not、or、
# concat、contains、strlen、substr


