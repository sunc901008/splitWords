<question> := <filter> |
              <phrase>;

<filter> := <number-simple-filter> |
            <date-simple-filter> |
            <date-complex-filter> |
            <string-simple-filter> |
            <string-complex-filter> |
            <bool-function-column>;

<phrase> := <top-n> |
            <bottom-n> |
            <sort-by> |
            <all-columns> |
            <number-complex-phrase> |
            <date-interval> |
            <formula-column>;

<number-simple-filter> := <number-columns> <bool-symbol> <number> |
                          <number> <bool-symbol> <number-columns> |
                          <number-columns> <bool-symbol> <number-columns> |
                          <number> <bool-symbol> <number>;

<number-complex-phrase> := <average-phrase>;

<date-simple-filter> := <date-columns> <bool-symbol> <date-string-value> |
                        <date-string-value> <bool-symbol> <date-columns> |
                        <date-columns> <bool-symbol> <date-columns> |
                        <date-string-value> <bool-symbol> <date-string-value>;

<date-complex-filter> := <before-after-filter> |
                         <last-filter>;

<equal-symbol> := 等于 |
                  =;
<string-simple-filter> := <all-string-column> <equal-symbol> <column-value>;

<string-complex-filter> := <begins-with-filter> |
                           <not-begins-with-filter> |
                           <contains-filter> |
                           <not-contains-filter>;

<top-n-chinese> := 前 |
                   排前 |
                   排名前;

<top-bottom-n-measure-word-chinese> := 的 |
                                       名的 |
                                       个的;

<top-n> := <top-n-chinese> <integer> <top-bottom-n-measure-word-chinese> <number-columns> |
           <number-columns> <top-n-chinese> <integer> <top-bottom-n-measure-word-chinese> |
           <top-1>;

<top-1-chinese> := 排名最前的 |
                   排最前的 |
                   最前的;

<top-1> := <top-1-chinese> <number-columns> |
           <number-columns> <top-1-chinese>;

<bottom-n-chinese> := 后 |
                      排后 |
                      排名后 |
                      倒数 |
                      倒数第;

<bottom-n> := <bottom-n-chinese> <integer> <top-bottom-n-measure-word-chinese> <number-columns> |
              <number-columns> <bottom-n-chinese> <integer> <top-bottom-n-measure-word-chinese> |
              <bottom-1>;

<bottom-1-chinese> := 排名最后的 |
                      排最后的 |
                      最后的;

<bottom-1> := <bottom-1-chinese> <number-columns> |
              <number-columns> <bottom-1-chinese>;

<sort-by> := 按 <all-columns> <sort-by-ascending> |
             按 <all-columns> <sort-by-descending> |
             按 <all-columns> <sort-by-chinese>;

<sort-by-chinese> := 排序 |
                     排序的 |
                     排列 |
                     排列的;

<sort-by-ascending> := 升序 |
                       升序的 |
                       升序排列 |
                       升序排列的 |
                       升序排序 |
                       升序排序的;

<sort-by-descending> := 降序 |
                        降序的 |
                        降序排列 |
                        降序排列的 |
                        降序排序 |
                        降序排序的;

<date-interval> := <daily-interval> |
                   <weekly-interval> |
                   <monthly-interval> |
                   <quarterly-interval> |
                   <yearly-interval>;

<daily-interval> := 每天 |
                    每日 |
                    日报;

<weekly-interval> := 每周 |
                     每星期 |
                     周报;

<monthly-interval> := 每月 |
                      月报;

<quarterly-interval> := 每季度 |
                        季度 |
                        每季 |
                        季报;

<yearly-interval> := 每年 |
                     年度 |
                     年报;

<average-phrase> := <number-source-column> 的平均值 |
                    平均 <number-source-column>;

<before-after-filter> := <before-filter> |
                         <after-filter>;

<before-chinese> := 之前 |
                    之前的 |
                    以前 |
                    以前的;

<before-filter> := 在 <date-columns> <before-chinese> |
                   <date-columns> <before-chinese> |
                   在 <date-string-value> <before-chinese> |
                   <date-string-value> <before-chinese> |
                   <all-date-column> 在 <date-columns> <before-chinese> |
                   <all-date-column> 在 <date-string-value> <before-chinese>;

<after-chinese> := 之后 |
                   之后的 |
                   以后 |
                   以后的;

<after-filter> := 在 <date-columns> <after-chinese> |
                  <date-columns> <after-chinese> |
                  在 <date-string-value> <after-chinese> |
                  <date-string-value> <after-chinese> |
                  <all-date-column> 在 <date-columns> <after-chinese> |
                  <all-date-column> 在 <date-string-value> <after-chinese>;

<last-filter> := <last-days-filter> |
                 <last-weeks-filter> |
                 <last-months-filter> |
                 <last-quarters-filter> |
                 <last-years-filter>;

<last-chinese> := 过去 |
                  前 |
                  最近 |
                  近;

<last-days-chinese> := 天 |
                       天的;
<last-days-filter> := <last-chinese> <integer> <last-days-chinese> |
                      <all-date-column> <last-chinese> <integer> <last-days-chinese>;

<last-weeks-chinese> := 周 |
                        周的 |
                        星期 |
                        星期的 |
                        个星期 |
                        个星期的;
<last-weeks-filter> := <last-chinese> <integer> <last-weeks-chinese> |
                       <all-date-column> <last-chinese> <integer> <last-weeks-chinese>;

<last-months-chinese> := 月 |
                         月的 |
                         个月 |
                         个月的;
<last-months-filter> := <last-chinese> <integer> <last-months-chinese> |
                        <all-date-column> <last-chinese> <integer> <last-months-chinese>;

<last-quarters-chinese> := 季度 |
                           季度的 |
                           季 |
                           季的 |
                           个季 |
                           个季的 |
                           个季度的 |
                           个季度的;
<last-quarters-filter> := <last-chinese> <integer> <last-quarters-chinese> |
                          <all-date-column> <last-chinese> <integer> <last-quarters-chinese>;

<last-years-chinese> := 年 |
                        年的;
<last-years-filter> := <last-chinese> <integer> <last-years-chinese> |
                       <all-date-column> <last-chinese> <integer> <last-years-chinese>;

<begins-with-filter> := <all-string-column> 开头是 <column-value> |
                        开头是 <column-value> 的 <all-string-column>;

<not-begins-with-filter> := <all-string-column> 开头不是 <column-value> |
                            开头不是 <column-value> 的 <all-string-column>;

<contains-filter> := <all-string-column> 包含 <column-value> |
                     包含 <column-value> 的 <all-string-column>;

<not-contains-filter> := <all-string-column> 不包含 <column-value> |
                         不包含 <column-value> 的 <all-string-column>;

<all-columns> := <number-columns> |
                 <string-columns> |
                 <bool-columns> |
                 <date-columns>;

<number-columns> := <all-int-column> |
                    <all-double-column>;

<string-columns> := <all-string-column>;

<bool-columns> := <all-bool-column>;

<date-columns> := <all-date-column>;

<all-int-column> := <int-column> |
                    <table-int-column>;

<all-double-column> := <double-column> |
                       <table-double-column>;

<all-string-column> := <string-column> |
                       <table-string-column>;

<all-bool-column> := <bool-column> |
                     <table-bool-column>;

<all-date-column> := <date-column> |
                     <table-date-column>;

<all-source-column> := <number-source-column> |
                       <all-string-column> |
                       <all-bool-column> |
                       <all-date-column>;

<number-source-column> := <all-int-column> |
                          <all-double-column>;

<bool-symbol> := 大于 | 小于 | 不小于 | 大于等于 | 不大于 | 小于等于 | 等于 | 不等于 | > | < | >= | <= | = | !=;
<math-symbol> := + | - | * | / | ^;
<number> := <integer> |
            <double>;
<integer> := 1;
<double> := 1;

<comma-symbol> := , |
                  ，;
<column-value> := <double-quote-column-value> |
                  <single-quote-column-value> |
                  <double-quote-column-value> <comma-symbol> <column-value> |
                  <single-quote-column-value> <comma-symbol> <column-value>;

<double-quote-column-value> := " <value> " |
                               “ <value> ”;
<single-quote-column-value> := ' <value> ' |
                               ‘ <value> ’;
<value> := 1;

<date-string-value> := <double-quote-date-string-value> |
                       <single-quote-date-string-value>;

<double-quote-date-string-value> := " <date-value> " |
                                    “ <date-value> ”;
<single-quote-date-string-value> := ' <date-value> ' |
                                    ‘ <date-value> ’;
<date-value> := 1;