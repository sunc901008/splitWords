<question> := <filter> |
              <phrase>;

<filter> := <number-simple-filter> |
            <date-simple-filter> |
            <date-complex-filter> |
            <string-simple-filter> |
            <string-complex-filter> |
            <bool-function-column>;

<phrase> := <top-n> |
            <bottom-n> |
            <sort-by> |
            <growth-of> |
            <all-columns> |
            <number-complex-phrase> |
            <other-function-columns>;

<number-simple-filter> := <number-columns> <bool-symbol> <number> |
                          <number> <bool-symbol> <number-columns> |
                          <number-columns> <bool-symbol> <number-columns> |
                          <number> <bool-symbol> <number>;

<number-complex-phrase> := <average-phrase> |
                           <sum-phrase> |
                           <count-phrase> |
                           <max-min-phrase> |
                           <standard-deviation-phrase> |
                           <unique-count-phrase> |
                           <variance-phrase>;

<date-simple-filter> := <date-columns> <bool-symbol> <date-string-value> |
                        <date-string-value> <bool-symbol> <date-columns> |
                        <date-columns> <bool-symbol> <date-columns> |
                        <date-string-value> <bool-symbol> <date-string-value>;

<date-complex-filter> := <before-after-filter> |
                         <last-filter> |
                         <next-filter> |
                         <between-and-filter> |
                         <date-interval> |
                         <ago-filter>;

<equal-symbol> := 等于 |
                  =;
<string-simple-filter> := <all-string-column> <equal-symbol> <column-value>;

<string-complex-filter> := <begins-with-filter> |
                           <not-begins-with-filter> |
                           <ends-with-filter> |
                           <not-ends-with-filter> |
                           <contains-filter> |
                           <not-contains-filter>;

<top-n-chinese> := 前 |
                   排前 |
                   排名前;

<top-bottom-n-measure-word-chinese> := 的 |
                                       名的 |
                                       个的;

<top-n> := <top-n-chinese> <integer> <top-bottom-n-measure-word-chinese> <number-columns> |
           <number-columns> <top-n-chinese> <integer> <top-bottom-n-measure-word-chinese> |
           <top-1>;

<top-1-chinese> := 排名最前的 |
                   排最前的 |
                   最前的;

<top-1> := <top-1-chinese> <number-columns> |
           <number-columns> <top-1-chinese>;

<bottom-n-chinese> := 后 |
                      排后 |
                      排名后 |
                      倒数 |
                      倒数第;

<bottom-n> := <bottom-n-chinese> <integer> <top-bottom-n-measure-word-chinese> <number-columns> |
              <number-columns> <bottom-n-chinese> <integer> <top-bottom-n-measure-word-chinese> |
              <bottom-1>;

<bottom-1-chinese> := 排名最后的 |
                      排最后的 |
                      最后的;

<bottom-1> := <bottom-1-chinese> <number-columns> |
              <number-columns> <bottom-1-chinese>;

<sort-by> := 按 <all-columns> <sort-by-ascending> |
             按 <all-columns> <sort-by-descending> |
             按 <all-columns> <sort-by-chinese>;

<sort-by-chinese> := 排序 |
                     排序的 |
                     排列 |
                     排列的;

<sort-by-ascending> := 升序 |
                       升序的 |
                       升序排列 |
                       升序排列的 |
                       升序排序 |
                       升序排序的;

<sort-by-descending> := 降序 |
                        降序的 |
                        降序排列 |
                        降序排列的 |
                        降序排序 |
                        降序排序的;

<growth-of> := 按 <all-date-column> 计算的 <growth-of-measure> <growth-of-by-date-interval> |
               <year-over-year> 按 <all-date-column> 计算的 <growth-of-measure> <growth-of-by-date-interval>;

<year-over-year> := 与往年同期相比;
<growth-of-measure> := <number-source-column> |
                       <number-source-column> <growth-of-measure-operation>;
<growth-of-measure-operation> := <growth-of-measure-sum-operation> |
                                 <growth-of-measure-average-operation> |
                                 <growth-of-measure-count-operation> |
                                 <growth-of-measure-max-operation> |
                                 <growth-of-measure-min-operation> |
                                 <growth-of-measure-standard-deviation-operation> |
                                 <growth-of-measure-unique-count-operation> |
                                 <growth-of-measure-variance-operation>;
<growth-of-measure-sum-operation> := 的总和 |
                                     的总量 |
                                     的总数;
<growth-of-measure-average-operation> := 的平均值;
<growth-of-measure-count-operation> := 的数量 |
                                       的计数;
<growth-of-measure-max-operation> := 的最大值;
<growth-of-measure-min-operation> := 的最小值;
<growth-of-measure-standard-deviation-operation> := 的标准差;
<growth-of-measure-unique-count-operation> := 去重后的数量;
<growth-of-measure-variance-operation> := 的方差;

<growth-of-by-date-interval> := <growth-of-daily-interval> |
                                <growth-of-weekly-interval> |
                                <growth-of-monthly-interval> |
                                <growth-of-quarterly-interval> |
                                <growth-of-yearly-interval>;
<growth-of-daily-interval> := 的日增长率;
<growth-of-weekly-interval> := 的周增长率;
<growth-of-monthly-interval> := 的增长率 |
                              的月增长率;
<growth-of-quarterly-interval> := 的季度增长率;
<growth-of-yearly-interval> := 的年增长率;

<date-interval> := <daily-interval> |
                   <weekly-interval> |
                   <monthly-interval> |
                   <quarterly-interval> |
                   <yearly-interval> |
                   <by-day-of-week-interval> |
                   <by-week-interval> |
                   <by-month-interval> |
                   <to-date-interval>;

<daily-interval> := 每天 |
                    每日 |
                    日报;

<weekly-interval> := 每周 |
                     每星期 |
                     周报;

<monthly-interval> := 每月 |
                      月报;

<quarterly-interval> := 每季度 |
                        季度 |
                        每季 |
                        季报;

<yearly-interval> := 每年 |
                     年度 |
                     年报;

<by-day-of-week-interval> := 按周日期统计 |
                             按周日期统计的 |
                             按每周七天统计 |
                             按每周七天统计的 |
                             按周分天统计 |
                             按周分天统计的;

<by-week-interval> := 按周统计 |
                      按周统计的;

<by-month-interval> := 按月统计 |
                       按月统计的;

<to-date-interval> := <today-interval> |
                      <week-to-date-interval> |
                      <month-to-date-interval> |
                      <quarter-to-date-interval> |
                      <year-to-date-interval>;

<today-interval> := 今天 |
                    今日;

<week-to-date-interval> := 本周截至目前 |
                             本周截至目前的 |
                             本周截至目前为止 |
                             本周截至目前为止的 |
                             当周截至目前 |
                             当周截至目前的 |
                             当周截至目前为止 |
                             当周截至目前为止的 |
                             本周截止目前 |
                             本周截止目前的 |
                             本周截止目前为止 |
                             本周截止目前为止的 |
                             当周截止目前 |
                             当周截止目前的 |
                             当周截止目前为止 |
                             当周截止目前为止的 |
                             本周 |
                             当周;

<month-to-date-interval> := 本月截至目前 |
                             本月截至目前的 |
                             本月截至目前为止 |
                             本月截至目前为止的 |
                             当月截至目前 |
                             当月截至目前的 |
                             当月截至目前为止 |
                             当月截至目前为止的 |
                             本月截止目前 |
                             本月截止目前的 |
                             本月截止目前为止 |
                             本月截止目前为止的 |
                             当月截止目前 |
                             当月截止目前的 |
                             当月截止目前为止 |
                             当月截止目前为止的 |
                             本月 |
                             当月;

<quarter-to-date-interval> := 本季度截至目前 |
                             本季度截至目前的 |
                             本季度截至目前为止 |
                             本季度截至目前为止的 |
                             当季度截至目前 |
                             当季度截至目前的 |
                             当季度截至目前为止 |
                             当季度截至目前为止的 |
                             本季度截止目前 |
                             本季度截止目前的 |
                             本季度截止目前为止 |
                             本季度截止目前为止的 |
                             当季度截止目前 |
                             当季度截止目前的 |
                             当季度截止目前为止 |
                             当季度截止目前为止的 |
                             本季度 |
                             当季度 |
                             本季截至目前 |
                             本季截至目前的 |
                             本季截至目前为止 |
                             本季截至目前为止的 |
                             当季截至目前 |
                             当季截至目前的 |
                             当季截至目前为止 |
                             当季截至目前为止的 |
                             本季截止目前 |
                             本季截止目前的 |
                             本季截止目前为止 |
                             本季截止目前为止的 |
                             当季截止目前 |
                             当季截止目前的 |
                             当季截止目前为止 |
                             当季截止目前为止的 |
                             本季 |
                             当季;

<year-to-date-interval> := 本年截至目前 |
                           本年截至目前的 |
                           本年截至目前为止 |
                           本年截至目前为止的 |
                           当年截至目前 |
                           当年截至目前的 |
                           当年截至目前为止 |
                           当年截至目前为止的 |
                           本年截止目前 |
                           本年截止目前的 |
                           本年截止目前为止 |
                           本年截止目前为止的 |
                           当年截止目前 |
                           当年截止目前的 |
                           当年截止目前为止 |
                           当年截止目前为止的 |
                           本年 |
                           当年 |
                           今年;

<ago-filter> := <ago-days-filter> |
               <ago-weeks-filter> |
               <ago-months-filter> |
               <ago-quarters-filter> |
               <ago-years-filter> |
               <ago-minutes-filter> |
               <ago-hours-filter>;

<ago-chinese> := 前 |
                 前的 |
                 以前 |
                 以前的;

<ago-day-chinese> := 天 |
                     日;

<ago-days-filter> := <integer> <ago-day-chinese> <ago-chinese>;

<ago-week-chinese> := 周 |
                      星期 |
                      个星期;

<ago-weeks-filter> := <integer> <ago-week-chinese> <ago-chinese>;

<ago-month-chinese> := 月 |
                       个月;

<ago-months-filter> := <integer> <ago-month-chinese> <ago-chinese>;

<ago-quarter-chinese> := 季度 |
                         个季度;

<ago-quarters-filter> := <integer> <ago-quarter-chinese> <ago-chinese>;

<ago-year-chinese> := 年;

<ago-years-filter> := <integer> <ago-year-chinese> <ago-chinese>;

<ago-minute-chinese> := 分 |
                        分钟;

<ago-minutes-filter> := <integer> <ago-minute-chinese> <ago-chinese>;

<ago-hour-chinese> := 小时 |
                      个小时;

<ago-hours-filter> := <integer> <ago-hour-chinese> <ago-chinese>;

<average-phrase> := <number-source-column> 的平均值 |
                    平均 <number-source-column>;

<sum-chinese> := 总和 |
                 总量 |
                 总数;
<sum-1-chinese> := 的总和 |
                   的总量 |
                   的总数;
<sum-phrase> := <number-source-column> <sum-1-chinese> |
                <sum-chinese> <number-source-column>;

<count-chinese> := 数量 |
                   的数量 |
                   计数 |
                   的计数;
<count-phrase> := <number-source-column> <count-chinese>;

<max-chinese> := 最大;
<max-1-chinese> := 最大值 |
                   的最大值;
<min-chinese> := 最小;
<min-1-chinese> := 最小值 |
                   的最小值;
<max-min-phrase> := <max-phrase> |
                    <min-phrase>;
<max-phrase> := <max-chinese> <number-source-column> |
                <number-source-column> <max-1-chinese>;
<min-phrase> := <min-chinese> <number-source-column> |
                <number-source-column> <min-1-chinese>;

<standard-deviation-chinese> := 标准差 |
                                的标准差;
<standard-deviation-phrase> := <number-source-column> <standard-deviation-chinese>;

<unique-count-chinese> := 去重后的数量 |
                          的去重后的数量;
<unique-count-phrase> := <number-source-column> <unique-count-chinese>;

<variance-chinese> := 方差 |
                      的方差;
<variance-phrase> := <number-source-column> <variance-chinese>;

<before-after-filter> := <before-filter> |
                         <after-filter>;

<before-chinese> := 之前 |
                    之前的 |
                    以前 |
                    以前的;

<before-filter> := 在 <date-columns> <before-chinese> |
                   <date-columns> <before-chinese> |
                   在 <date-string-value> <before-chinese> |
                   <date-string-value> <before-chinese> |
                   <all-date-column> 在 <date-columns> <before-chinese> |
                   <all-date-column> 在 <date-string-value> <before-chinese>;

<after-chinese> := 之后 |
                   之后的 |
                   以后 |
                   以后的;

<after-filter> := 在 <date-columns> <after-chinese> |
                  <date-columns> <after-chinese> |
                  在 <date-string-value> <after-chinese> |
                  <date-string-value> <after-chinese> |
                  <all-date-column> 在 <date-columns> <after-chinese> |
                  <all-date-column> 在 <date-string-value> <after-chinese>;

<last-filter> := <last-days-filter> |
                 <last-weeks-filter> |
                 <last-months-filter> |
                 <last-quarters-filter> |
                 <last-years-filter> |
                 <last-minutes-filter> |
                 <last-hours-filter>;

<last-chinese> := 过去 |
                  前 |
                  最近 |
                  近;

<days-chinese> := 天 |
                       天的;
<last-days-filter> := <last-chinese> <integer> <days-chinese> |
                      <all-date-column> <last-chinese> <integer> <days-chinese> |
                      <all-date-column> <last-day-filter> |
                      <last-day-filter>;
<last-day-filter> := 昨天 |
                     最后一天 |
                     前一天;

<weeks-chinese> := 周 |
                    周的 |
                    星期 |
                    星期的 |
                    个星期 |
                    个星期的;
<last-weeks-filter> := <last-chinese> <integer> <weeks-chinese> |
                       <all-date-column> <last-chinese> <integer> <weeks-chinese> |
                       <all-date-column> <last-week-filter> |
                       <last-week-filter>;
<last-week-filter> := 上周 |
                      上个星期 |
                      前一周;

<months-chinese> := 月 |
                         月的 |
                         个月 |
                         个月的;
<last-months-filter> := <last-chinese> <integer> <months-chinese> |
                        <all-date-column> <last-chinese> <integer> <months-chinese> |
                        <all-date-column> <last-month-filter> |
                        <last-month-filter>;
<last-month-filter> := 上一个月 |
                       上个月 |
                       上月 |
                       前一个月;

<quarters-chinese> := 季度 |
                           季度的 |
                           季 |
                           季的 |
                           个季 |
                           个季的 |
                           个季度的 |
                           个季度的;
<last-quarters-filter> := <last-chinese> <integer> <quarters-chinese> |
                          <all-date-column> <last-chinese> <integer> <quarters-chinese> |
                          <all-date-column> <last-quarter-filter> |
                          <last-quarter-filter>;
<last-quarter-filter> := 上个季度 |
                         上季度 |
                         上一季度;

<years-chinese> := 年 |
                    年的;
<last-years-filter> := <last-chinese> <integer> <years-chinese> |
                       <all-date-column> <last-chinese> <integer> <years-chinese> |
                       <all-date-column> <last-year-filter> |
                       <last-year-filter>;
<last-year-filter> := 去年 |
                      上一年 |
                      前一年;

<minutes-chinese> := 分 |
                      分的 |
                      分钟 |
                      分钟的;
<last-minutes-filter> := <last-chinese> <integer> <minutes-chinese> |
                         <all-date-column> <last-chinese> <integer> <minutes-chinese> |
                         <all-date-column> <last-minute-filter> |
                         <last-minute-filter>;
<last-minute-filter> := 前一分钟 |
                        上一分钟;

<hours-chinese> := 小时 |
                    小时的 |
                    个小时 |
                    个小时的;
<last-hours-filter> := <last-chinese> <integer> <hours-chinese> |
                       <all-date-column> <last-chinese> <integer> <hours-chinese> |
                       <all-date-column> <last-hour-filter> |
                       <last-hour-filter>;
<last-hour-filter> := 前一小时 |
                      上一小时 |
                      前一个小时 |
                      上一个小时
                      上个小时;

<next-filter> := <next-days-filter> |
                 <next-weeks-filter> |
                 <next-months-filter> |
                 <next-quarters-filter> |
                 <next-years-filter> |
                 <next-minutes-filter> |
                 <next-hours-filter>;

<next-chinese> := 未来 |
                  未来的 |
                  接下来 |
                  接下来的;

<next-days-filter> := <next-chinese> <integer> <days-chinese> |
                      <all-date-column> <next-chinese> <integer> <days-chinese> |
                      <all-date-column> <next-day-filter> |
                      <next-day-filter>;
<next-day-filter> := 明天 |
                     翌日 |
                     次日;

<next-weeks-filter> := <next-chinese> <integer> <weeks-chinese> |
                       <all-date-column> <next-chinese> <integer> <weeks-chinese> |
                       <all-date-column> <next-week-filter> |
                       <next-week-filter>;
<next-week-filter> := 下周 |
                      下个星期 |
                      下一周;

<next-months-filter> := <next-chinese> <integer> <months-chinese> |
                        <all-date-column> <next-chinese> <integer> <months-chinese> |
                        <all-date-column> <next-month-filter> |
                        <next-month-filter>;
<next-month-filter> := 下一个月 |
                       下个月 |
                       下月 |
                       未来一个月 |
                       下一个月的 |
                       下个月的 |
                       下月的 |
                       未来一个月的;

<next-quarters-filter> := <next-chinese> <integer> <quarters-chinese> |
                          <all-date-column> <next-chinese> <integer> <quarters-chinese> |
                          <all-date-column> <next-quarter-filter> |
                          <next-quarter-filter>;
<next-quarter-filter> := 下个季度 |
                         下季度 |
                         下一季度;

<next-years-filter> := <next-chinese> <integer> <years-chinese> |
                       <all-date-column> <next-chinese> <integer> <years-chinese> |
                       <all-date-column> <next-year-filter> |
                       <next-year-filter>;
<next-year-filter> := 明年 |
                      下一年 |
                      来年 |
                      下一年度;

<next-minutes-filter> := <next-chinese> <integer> <minutes-chinese> |
                         <all-date-column> <next-chinese> <integer> <minutes-chinese> |
                         <all-date-column> <next-minute-filter> |
                         <next-minute-filter>;
<next-minute-filter> := 下一分钟;

<next-hours-filter> := <next-chinese> <integer> <hours-chinese> |
                       <all-date-column> <next-chinese> <integer> <hours-chinese> |
                       <all-date-column> <next-hour-filter> |
                       <next-hour-filter>;
<next-hour-filter> := 下一小时 |
                      下个小时;

<between-and-filter> := <all-date-column> 在 <date-string-value> 和 <date-string-value> 之间的 |
                        在 <date-string-value> 和 <date-string-value> 之间的 |
                        <date-string-value> 和 <date-string-value> 之间的 |
                        <number-source-column> 在 <date-string-value> 和 <date-string-value> 之间 |
                        <number-source-column> 在 <date-string-value> 和 <date-string-value> 之间的 |
                        <between-and-1-filter>;

<between-and-1-filter> := <number-source-column> 在 <number> 和 <number> 之间 |
                        在 <number> 和 <number> 之间的 <number-source-column>;

<begins-with-filter> := <all-string-column> 开头是 <column-value> |
                        开头是 <column-value> 的 <all-string-column>;

<not-begins-with-filter> := <all-string-column> 开头不是 <column-value> |
                            开头不是 <column-value> 的 <all-string-column>;

<ends-with-filter> := <all-string-column> 结尾是 <column-value> |
                      结尾是 <column-value> 的 <all-string-column>;

<not-ends-with-filter> := <all-string-column> 结尾不是 <column-value> |
                          结尾不是 <column-value> 的 <all-string-column>;

<contains-filter> := <all-string-column> 包含 <column-value> |
                     包含 <column-value> 的 <all-string-column>;

<not-contains-filter> := <all-string-column> 不包含 <column-value> |
                         不包含 <column-value> 的 <all-string-column>;

<all-columns> := <number-columns> |
                 <string-columns> |
                 <bool-columns> |
                 <date-columns>;

<number-columns> := <all-int-column> |
                    <all-double-column> |
                    <number-function-column> |
                    ( <number-function-column> );;

<string-columns> := <all-string-column> |
                    <string-function-column> |
                    ( <string-function-column> );

<bool-columns> := <all-bool-column>;

<date-columns> := <all-date-column> |
                  <date-function-column> |
                  ( <date-function-column> );

<all-int-column> := <int-column> |
                    <table-int-column>;

<all-double-column> := <double-column> |
                       <table-double-column> |
                       <number-formula-column>;

<all-string-column> := <string-column> |
                       <table-string-column> |
                       <string-formula-column>;

<all-bool-column> := <boolean-column> |
                     <table-boolean-column> |
                     <bool-formula-column>;

<all-date-column> := <timestamp-column> |
                     <table-timestamp-column> |
                     <date-formula-column>;

<all-source-column> := <not-number-source-column> |
                       <number-source-column>;

<not-number-source-column> := <all-string-column> |
                              <all-bool-column> |
                              <all-date-column>;

<number-source-column> := <all-int-column> |
                          <all-double-column> |
                          <number-formula-column>;

<bool-symbol> := 大于 | 小于 | 不小于 | 大于等于 | 不大于 | 小于等于 | 等于 | 不等于 | > | < | >= | <= | = | !=;
<math-symbol> := + | - | * | / | ^;
<number> := <integer> |
            <double>;
<integer> := 1;
<double> := 1;

<comma-symbol> := , |
                  ，|
                  、;
<column-value> := <single-column-value> |
                  <single-column-value> <comma-symbol> <column-value>;

<single-column-value> := <double-quote-column-value> |
                         <single-quote-column-value>;

<double-quote-column-value> := " <value> " |
                               “ <value> ”;
<single-quote-column-value> := ' <value> ' |
                               ‘ <value> ’;
<value> := 1;

<date-string-value> := <double-quote-date-string-value> |
                       <single-quote-date-string-value>;

<double-quote-date-string-value> := " <date-value> " |
                                    “ <date-value> ”;
<single-quote-date-string-value> := ' <date-value> ' |
                                    ‘ <date-value> ’;
<date-value> := 1;