<question> := <filter> |
              <phrase>;

<filter> := <simple-filter> |
            <column-value>;

<phrase> := <top-n> |
            <bottom-n> |
            <sort-by> |
            <growth-of> |
            <all-columns>;

<simple-filter> := <number-columns> <bool-symbol> <number> |
                   <number> <bool-symbol> <number-columns> |
                   <number-columns> <bool-symbol> <number-columns> |
                   <number> <bool-symbol> <number> |
                   <bool-function-column>;

<top-n> := top <integer> <number-columns> |
           top <number-columns>;

<bottom-n> := bottom <integer> <number-columns> |
              bottom <number-columns>;

<sort-by> := sort by <all-columns> |
             sort by <all-columns> desc |
             sort by <all-columns> asc;

<growth-of> := growth of <number-columns> by <date-columns> |
               growth of <number-columns> by <date-columns> <year-over-year> |
               growth of <number-columns> by <date-columns> <growth-of-by-date-interval> |
               growth of <number-columns> by <date-columns> <growth-of-by-date-interval> <year-over-year>;

<growth-of-by-date-interval> := daily |
                                weekly |
                                monthly |
                                quarterly |
                                yearly;

<year-over-year> := year over year;

<all-columns> := <number-columns> |
                 <string-columns> |
                 <bool-columns> |
                 <date-columns>;

<number-columns> := <all-int-column> |
                    <all-double-column> |
                    <number-function-column>;

<string-columns> := <all-string-column> |
                    <string-function-column>;

<bool-columns> := <all-bool-column>;

<date-columns> := <all-date-column> |
                  <date-function-column>;

<all-int-column> := <int-column> |
                    <table-int-column>;

<all-double-column> := <double-column> |
                       <table-double-column>;

<all-string-column> := <string-column> |
                       <table-string-column>;

<all-bool-column> := <bool-column> |
                     <table-bool-column>;

<all-date-column> := <date-column> |
                     <table-date-column>;

<all-source-column> := <number-source-column> |
                       <all-string-column> |
                       <all-bool-column> |
                       <all-date-column>;

<number-source-column> := <all-int-column> |
                          <all-double-column>;

<symbol> := <bool-symbol> |
            <math-symbol>;

<bool-symbol> := > | < | >= | <= | = | !=;
<math-symbol> := + | - | * | / | ^;
<number> := <integer> |
            <double>;
<integer> := 1;
<double> := 1;

<column-value> := <double-quote-column-value> |
                  <single-quote-column-value> |
                  <double-quote-column-value> , <column-value> |
                  <single-quote-column-value> , <column-value>;

<double-quote-column-value> := " <value> ";
<single-quote-column-value> := ' <value> ';
<value> := 1;
